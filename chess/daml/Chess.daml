-- Copyright (c) 2019 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

daml 1.2
module Chess where


import Rules.Draw
import Rules.Data
import Rules.Main
import Types

type GameId = Text
type GameContractIds = (ContractId ActiveSideOfGame, ContractId PassiveSideOfGame)

template GameProposal
  with
    gameId : GameId
    proposer : Party
    desiredSide : Side
    opponent : Party
    operator : Party
  where
    signatory proposer
    -- Only one game from proposer with the same id
    key (proposer, gameId) : (Party, GameId)
    maintainer key._1

    controller opponent can
      Accept : ContractId GameAccept
        do create GameAccept with ..

template GameAccept
  with
    gameId : GameId
    proposer : Party
    desiredSide : Side
    opponent : Party
    operator : Party
  where
    signatory proposer, opponent

    controller operator can
      Start : ContractId Game
        do create Game with ..

template Game
  with
    gameId : GameId
    proposer : Party
    desiredSide : Side
    opponent : Party
    operator : Party
  where
    signatory proposer, opponent, operator
    key (operator, gameId) : (Party, GameId)
    maintainer key._1
    controller operator can
      nonconsuming Begin : GameContractIds
        do
          let (activeSide, passiveSide) =
                case desiredSide of
                  White -> (proposer, opponent)
                  Black -> (opponent, proposer)

          activeId <- create ActiveSideOfGame with
                          player = activeSide
                          opponent = passiveSide
                          active = Rules.Data.initSplitState White
                          side = White
                          ..
          passiveId <- create PassiveSideOfGame with
                          player = passiveSide
                          opponent = activeSide
                          passive = Rules.Data.initSplitState Black
                          side = Black
                          ..
          return (activeId, passiveId)

      End : ContractId GameResult
        with
          game : Optional GameState
          drawOrWinner : DrawOrWinner Party
        do
          create GameResult with ..

template GameResult
  with
    opponent : Party
    proposer : Party
    gameId : GameId
    operator : Party
    game : Optional GameState
    drawOrWinner : DrawOrWinner Party
  where
    signatory proposer, opponent, operator

type SideKey = (Party, GameId, Party)

template PassiveSideOfGame
  with
    player : Party
    side : Side
    operator : Party
    opponent : Party
    gameId : GameId
    passive : GameState
  where
    signatory player, operator
    key (operator, gameId, player) : SideKey
    maintainer key._1

    controller operator can
      Activate : ContractId ActiveSideOfGame
        with
          active : GameState
        do create ActiveSideOfGame with ..
      DeclareWinnerPassive : ContractId GameResult --
        with
          result : ContractId GameResult
        do return result

    controller player can
      Resign : ContractId Surrender
        do create Surrender with ..
      AskForADraw : ContractId DrawRequest
        do create DrawRequest with ..

-- It is the active side's turn.
template ActiveSideOfGame
  with
    player : Party
    side : Side
    operator : Party
    opponent : Party
    gameId : GameId
    active : GameState
  where
    signatory player, operator
    key (operator, gameId, player) : SideKey
    maintainer key._1

    controller player can
      nonconsuming Move : ContractId ActiveAction
        with
          move : ChessMove
        do
          create ActiveAction with move = Some move, ..

      nonconsuming ClaimDraw : ContractId ActiveAction
        do
          create ActiveAction with move = None, ..


      Forfeit : ContractId Surrender
        do
          create Surrender with ..

    controller operator can
      -- operator can only take this action after receiving a unique Move,
      Inactivate : ContractId PassiveSideOfGame
        with
          passive : GameState
        do
          create PassiveSideOfGame with ..

      DeclareWinnerActive : ContractId GameResult --
        with
          result : ContractId GameResult
        do
          return result

template Surrender
  with
    player : Party
    operator : Party
    gameId : GameId
    opponent : Party
  where
    signatory operator, player

    key (operator, gameId) : (Party, GameId)
    maintainer key._1

template DrawRequest
  with
    player : Party
    operator : Party
    gameId : GameId
    opponent : Party
    side : Side
    -- reveal : Bool -- add?
  where
    signatory operator, player

    key (operator, gameId, player) : (Party, GameId, Party)
    maintainer key._1

    controller opponent can
      AcceptDrawRequest : ContractId AcceptedDraw
        do create AcceptedDraw with ..

template AcceptedDraw
  with
    player : Party
    operator : Party
    gameId : GameId
    opponent : Party
    side : Side
  where
    signatory player, opponent
    key (operator, gameId) : (Party, GameId)
    maintainer key._1

template ActiveAction
  with
    player : Party
    operator : Party
    gameId : GameId
    opponent : Party
    move : Optional ChessMove     -- None = ClaimDraw
  where
    signatory operator

    key (operator, gameId) : (Party, GameId)
    maintainer key._1

template OperatorRole
  with
    operator : Party
    gameId : GameId
  where
    signatory operator

    key (operator, gameId) : (Party, GameId)
    maintainer key._1

    controller operator can
      -- operator's reaction to the existence of an ActiveAction
      nonconsuming AdvancePlay : Either (ContractId GameResult) GameContractIds
        do
          -- Look up move information from ActiveAction and archive it
          (amId, action) <- fetchByKey @ActiveAction (operator, gameId)

          let player = action.player
              opponent = action.opponent
              move = action.move
          exercise amId Archive

          -- figure out global state
          (activeId, activeState) <- fetchByKey @ActiveSideOfGame (operator, gameId, player)
          (passiveId, passiveState) <- fetchByKey @PassiveSideOfGame (operator, gameId, opponent)
          let gameState = if activeState.side == White
                          then joinGameStates activeState.active passiveState.passive
                          else joinGameStates passiveState.passive activeState.active
              moveSide = activeState.side
          case move of
            None  ->  -- claimDraw?
              case checkDraw gameState.moves of
                Left e -> abort e
                Right None ->
                  do -- not a draw
                    return $ Right (activeId, passiveId)
                Right (Some drawType) ->
                  do
                    let drawOrWinner = Draw drawType
                    (gameStartId, gameStart) <- fetchByKey @Game (operator, gameId)
                    gameResultId <- exercise gameStartId End with game = Some gameState, ..
                    exercise activeId DeclareWinnerActive with result = gameResultId
                    exercise passiveId DeclareWinnerPassive with result = gameResultId
                    return $ Left gameResultId

            Some m ->
              case tryMove gameState m of
                -- determine potential winner
                DrawOrWinner dow ->
                  let drawOrWinner =
                        case dow of
                          Draw dt -> Draw dt
                          Winner side -> if side == moveSide then Winner activeState.player else Winner activeState.opponent
                      game = (gameState with moves = m :: gameState.moves)
                  in
                  do
                    (gameStartId, gameStart) <- fetchByKey @Game (operator, gameId)
                    gameResultId <- exercise gameStartId End with game = Some game, ..
                    exercise activeId DeclareWinnerActive with result = gameResultId
                    exercise passiveId DeclareWinnerPassive with result = gameResultId
                    return $ Left gameResultId
                Error e ->
                  do
                    -- abort e  -- What mechanism exists to warn to alert the user?
                    return $ Right (activeId, passiveId)
                -- alternate states
                NewState newJoinedState ->
                  let whiteState = splitGameStateOfSide White newJoinedState
                      blackState = splitGameStateOfSide Black newJoinedState
                      (active, passive) =
                        case moveSide of
                          White -> (blackState, whiteState)
                          Black -> (whiteState, blackState)
                  in
                  do
                    passiveId' <- exercise activeId Inactivate with passive
                    activeId' <- exercise passiveId Activate with active
                    return $ Right (activeId', passiveId')

      -- operator's reaction to the existence of an Surrender
      nonconsuming AcknowledgeSurrender : ContractId GameResult
        do
          (sId, surrender) <- fetchByKey @Surrender (operator, gameId)
          (gameStartId, _gameStart) <- fetchByKey @Game (operator, gameId)
          archive sId
          exercise gameStartId End with game = None, drawOrWinner = Winner surrender.opponent, ..

      nonconsuming AcknowledgeAcceptedDraw : ContractId GameResult
        do
          (adId, acceptedDraw) <- fetchByKey @AcceptedDraw (operator, gameId)
          (gameStartId, _gameStart) <- fetchByKey @Game (operator, gameId)
          archive adId
          exercise gameStartId End with game = None, drawOrWinner = Draw (PlayerDraw acceptedDraw.side)
